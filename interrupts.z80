set_im2:
    di
    ld hl, 0xFE00
    ld de, 0xFE01
    ld bc, 0x101
    ld a, d
    ld i, a
    ld (hl), 0xfd
    ldir
    ld hl, 0xfdfd
    ld (hl), 0xc3
    inc l
    ld (hl), low(routine)
    inc l
    ld (hl), high(routine)
    im 2
    ei
    ret

routine:
    di

    push af
    push bc
    push de
    push hl
    push ix
    push iy

    ex af, af'
    exx

    push af
    push bc
    push de
    push hl

    ld a, (dirty_screen)
    or a
    jr z, .skip_map_redraw

    ld a, 1
    out (254), a

    call recover_buffer

    ld hl, objects

.objects_loop:
    push hl
    ld a, (hl)
    inc hl
    ld h, (hl)
    ld l, a
    xor a
    or h
    jr z, 1F
	call draw_object
    pop hl
    .2 inc hl
    jr .objects_loop

1   pop hl

    xor a
    ld bc, 0x3ff
1
    dec bc
    cp b
    jr nz, 1B

    call copy_buffer
    xor a
    ld (dirty_screen), a

    ; todo: reset full redraw

.skip_map_redraw:
    ld a, 0
    out (254), a

    pop hl
    pop de
    pop bc
    pop af

    ex af, af'
    exx

    pop iy
    pop ix
    pop hl
    pop de
    pop bc
    pop af

    ei
    reti


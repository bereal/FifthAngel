
    struct object
y           byte
x           byte
mirror byte
sprite      word
state       word
spritesheet word
behaviour word
    ends

    struct object_behaviour
tick word
turn word
interact word
draw word
    ends

    module objects

SPRITE_SIZE equ 53

mult_table
    rept 7, idx
    dw idx * SPRITE_SIZE
    endr

; hl - object list (preserved)
; a - behaviour offset
object_loop:
1
    push hl
    ld b, a
    ld a, (hl)
    inc hl
    ld h, (hl)
    ld l, a
    xor a
    or h
    jr z, 2F
    ld a, b
    push af
    call call_behaviour
    pop af
    pop hl
    .2 inc hl
    jr 1B
2
    pop hl
    ret

; hl - object
; a - behaviour offset
; corrupts de and bc
call_behaviour:
    ld d, h
    ld e, l
    ld bc, object.behaviour
    add hl, bc
    ld c, (hl)
    inc hl
    ld h, (hl)
    bit 7, h
    ret z
    ld l, c

    ld c, a
    add hl, bc
    ld c, (hl)
    inc hl
    ld h, (hl)
    bit 7, h
    ret z
    ld l, c

    push hl
    ex de, hl
    ret

;; ix - character (preserved)
;; a - sprite number
;; corrupts: hl, de
update_sprite:
    ld e, a
    ld d, 0
    ld hl, mult_table
    add hl, de
    add hl, de
    ld e, (hl)
    inc hl
    ld h, (hl)
    ld l, e

    ld e, (ix + object.spritesheet)
    ld d, (ix + object.spritesheet + 1)

    add hl, de

    ld (ix + object.sprite), l
    ld (ix + object.sprite + 1), h

    ld a, 1
    ld (dirty_screen), a
    ret


    endmodule
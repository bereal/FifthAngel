    module inventory


    struct inventory_item
icon word
name word
description word
    ends

keycard inventory_item res.keycard, i18n.keycard, i18n.keycard_description

; a - item index
add_item:
    ld hl, INVENTORY_START
    ld b, a
1
    ld a, (hl)
    or a
    jr z, 2F
    inc hl
    jr 1B
2

    ld (hl), b
    ret

inventory_items:
    dw keycard

; a - item code (overwritten)
; out: hl - item
resolve_item:
    push bc
    ld c, a
    dec c
    ld b, 0
    ld hl, inventory_items
    add hl, bc
    add hl, bc
    ld a, (hl)
    inc hl
    ld h, (hl)
    ld l, a
    pop bc
    ret


; de - icon addr (using sprite format)
; hl - screen coords (row /col)
draw_icon:
    call screen.pos_to_bitmap_addr
    ex de, hl

    inc hl

    ld (.restore_sp+1), sp
    ld sp, hl
    ex de, hl

    ld b, 8
    ld a, h
    inc sp
1
    pop de
    ld (hl), d
    inc h
    djnz 1B

    ld h, a
    inc l
    ld b, 8

    inc sp
2
    pop de
    ld (hl), d
    inc h
    djnz 2B

    dec l
    correct_down_line h, l

    ld a, h

    inc sp
    rept 4, idx
    pop de
    ld (hl), e
    inc h
    ld (hl), d
    if idx < 3
        inc h
    endif
    endr

    ld h, a
    inc l

    inc sp
    rept 4, idx
    pop de
    ld (hl), e
    inc h
    ld (hl), d
    if idx < 3
        inc h
    endif
    endr

.restore_sp:
    ld sp, 0
    ret


    endmodule
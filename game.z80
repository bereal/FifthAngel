    module game

    define SpriteSize      49
    define FaceStand       0
    define FaceWalk        1
    define BackStand       2
    define BackWalk        3
    define SideStand       4
    define SideWalk1       5
    define SideWalk2       6

    define DirectionFace    0b000
    define DirectionBack    0b010
    define DirectionRight   0b001
    define DirectionLeft    0b011


    struct object

y           byte
x           byte
mirror byte
sprite      word
state       word
spritesheet word
behaviour word
    ends

total_steps dw 0

hero object 1, 1, 0, girl, 0, girl, 0
door_1 object 8, 4, 0, door, 0, door, door_behaviour
door_2 object 14, 4, 0, door, 0, door, door_behaviour
door_3 object 14, 22, 0, door, 0, door, door_behaviour

; sets Z if no object found
find_next_drawable_object:
    ld hl, objects
    ld b, 0xff ; best y
    ld de, 0 ; best object

.loop
    ld c, (hl)
    inc hl
    ld a, (hl)
    or a
    jr z, .end_loop

    bit 7, a
    jr z, .next_object

    push de
    ld e, c
    ld d, a
    ld a, (de)
    cp b
    jr nc, .next_object_pop

    ld b, a ; new best y
    pop af ; discard the previous object from the stack
    ld ix, hl
    inc hl
    jr .loop
.next_object_pop
    pop de
.next_object
    inc hl
    jr .loop

.end_loop
    ld a, d
    or a  ; set Z if no object found
    ret z

    ex de, hl
    ld a, (ix)
    and 0x7f
    ld (ix), a
    ret

draw_objects:
    ld hl, objects

.draw_loop
    call find_next_drawable_object
    jr z, .clear
    call map.draw_object
    jr .draw_loop

.clear
    ld hl, objects
.clear_loop
    inc hl
    ld a, (hl)
    or a
    ret z
    or 0x80
    ld (hl), a
    inc hl
    jr .clear_loop

mult_table:
    rept 7, idx
    dw idx * SpriteSize
    endr

;; ix - character (preserved)
;; a - sprite number
;; corrupts: hl, de
update_sprite:
    ld e, a
    ld d, 0
    ld hl, mult_table
    add hl, de
    add hl, de
    ld e, (hl)
    inc hl
    ld h, (hl)
    ld l, e

    ld e, (ix + object.spritesheet)
    ld d, (ix + object.spritesheet + 1)

    add hl, de

    ld (ix + object.sprite), l
    ld (ix + object.sprite + 1), h

    ld a, 1
    ld (dirty_screen), a
    ret

stop_hero:
    ld a, (hero.state)
    or a
    ret z

    xor a
    ld (hero.state), a
    ld a, (hero.state+1)   ; direction

    cp DirectionFace
    jr nz, 1F

    xor a
    ld e, a
    jr .update

1
    cp DirectionBack
    jr nz, 2F
    xor a
    ld e, BackStand
    jr .update

2
    ld e, SideStand
    rra

.update
    ld ix, hero
    ; ld (ix + object.mirror), 0
    ld a, e
    call update_sprite

    ld (ix + object.state), 0
    ret

move_hero_right:
    ld a, (hero.state + 1)
    cp DirectionRight
    jr z, 1F

    ; init moving state
    xor a
    ld (hero.state), a
    xor a
    ld (hero.mirror), a

    ld a, DirectionRight
    ld (hero.state + 1), a

    ld a, 1
    ld e, SideStand
    jr .update

1
    ld a, (hero.state)
    and 0xf
    cp 1
    jr nz, 2F

    ld a, 2
    ld e, SideWalk1
    jr .update

2
    call map.move_hero_right
    call log_steps
    ld a, 1
    ld e, SideWalk2

.update
    ld ix, hero
    ld (ix + object.state), a
    ld a, e
    call update_sprite
    ret


move_hero_left:
    ld a, (hero.state + 1)
    cp DirectionLeft
    jr z, 1F

    ; init moving state
    ld a, DirectionLeft
    ld (hero.state + 1), a

    ld a, 1
    ld  (hero.mirror), a
    ld e, SideStand
    jr .update

1
    ld a, (hero.state)
    and 0xf
    cp 1
    jr nz, 2F

    ld a, 2
    ld e, SideWalk1
    jr .update

2
    call map.move_hero_left
    call log_steps
    ld a, 1
    ld e, SideWalk2

.update
    ld ix, hero
    ld (ix + object.state), a
    ld a, e
    call update_sprite
    ret

move_hero_down:
    ld ix, hero

    ld a, (hero.state + 1)
    cp DirectionFace
    jr z, 1F

    ; init moving state
    ld a, DirectionFace
    ld (hero.state + 1), a

    xor a
    ld (hero.mirror), a
    inc a
    ld de, FaceStand
    jr .update

1
    ld a, (hero.state)
    and 0xf
    cp 1
    jr nz, 2F

    ld a, 2
    ld de, FaceWalk
    jr .update

2
    call map.move_hero_down
    call log_steps
    ld a, 1
    ld de, 0x100 + FaceWalk

.update
    ld (ix + object.state), a
    ld (ix + object.mirror), d
    ld a, e
    call update_sprite
    ret

move_hero_up:
    ld ix, hero

    ld a, (hero.state + 1)
    cp DirectionBack
    jr z, 1F

    ; init moving state
    ld a, DirectionBack
    ld (hero.state + 1), a

    xor a
    ld (hero.mirror), a
    ld de, BackStand
    inc a
    jr .update

1
    ld a, (hero.state)
    and 0xf
    cp 1
    jr nz, 2F

    ld a, 2
    ld de, BackWalk
    jr .update

2
    call map.move_hero_up
    call log_steps
    ld a, 1
    ld de, 0x100 + BackWalk

.update
    ld (ix + object.state), a
    ld (ix + object.mirror), d
    ld a, e
    jp update_sprite


log_steps:
	ld hl, game.total_steps
	inc (hl)
	ld hl, i18n.step
    push ix
    ld ix, default_console
	call console.println
    pop ix
    ret


door_behaviour:
    ld a, (ix + object.state)
    or a
    jr z, .closed

    cp 0xff
    jr z, .open

    bit 7, a
    jr z, .opening

.closing
    and 0x0f
    or a
    jr nz, 1F
    ; ld hl, i18n.door_closed
    ; call console.print_localized
    ld (ix + object.state), 0
    ld a, 1
    ld (dirty_screen), a
    ret

1
    bit 0, a
    jr z, 2F
    dec a
    or 0x80
    ld (ix + object.state), a
    ret

2
    dec a
    or 0x80
    ld (ix + object.state), a
    and 0x7f
    sra a
    call update_sprite
    ret

.closed
    ld a, (hero.x)
    cp (ix + object.x)
    ret nz

    ld a, (hero.y)
    sub (ix + object.y)
    cp 1
    jr z, .start_opening
    neg
    cp 1
    ret nz

.start_opening
    ld (ix + object.state), 1
    ret

.open
    ld a, (hero.x)
    cp (ix + object.x)
    jr nz, .start_closing

    ld a, (ix + object.y)
    and 0x7f
    ld b, a
    ld a, (hero.y)
    sub b
    ret z
    cp 1
    ret z
    neg
    cp 1
    ret z

.start_closing
    ld (ix + object.state), 0x87
    ld a, (ix)
    and 0x7f
    ld (ix), a ; unhide object
    ret

.opening
    cp 5
    jr nz, 1F
    ; done opening, deactivate the door
    ; ld hl, i18n.door_open
    ; push ix
    ; call console.print_localized
    ; pop ix
    ld a, (ix)
    or 0x80
    ld (ix), a ; hide object by moving it off-screen
    ld (ix + object.state), 0xff
    ld a, 1
    ld (dirty_screen), a
    ret

1
    bit 0, a
    jr z, 2F
    inc a
    ld (ix + object.state), a
    ret

2
    inc a
    ld (ix + object.state), a
    sra a
    call update_sprite
    ret

    endmodule
